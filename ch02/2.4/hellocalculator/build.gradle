apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = '1.8' // For Java 8

mainClassName = 'hellocalculator.HelloCalculator'

repositories {
  mavenCentral()
}

version = '0.2.0' // Bumped version

dependencies {
  compile('org.apache.kafka:kafka_2.9.2:0.8.1.1') {
    exclude group: 'javax.jms', module: 'jms'
    exclude group: 'com.sun.jdmk', module: 'jmxtools'
    exclude group: 'com.sun.jmx', module: 'jmxri'
  }
  compile 'com.google.code.gson:gson:2.2.4'
  compile 'com.github.fge:json-schema-validator:2.2.3'
  compile 'org.slf4j:slf4j-api:1.7.5'
  testCompile 'junit:junit:4.11'
}

jar {
  from(configurations.compile.collect {
    it.isDirectory() ? it : zipTree(it)
    }) {
      exclude "META-INF/*.SF"
      exclude "META-INF/*.DSA"
      exclude "META-INF/*.RSA"
    }

  manifest {
    attributes 'Implementation-Title': 'HelloCalculator',
      'Implementation-Version': version,
      'Built-By': System.getProperty('user.name'),
      'Built-Date': new Date(),
      'Built-JDK': System.getProperty('java.version'),
      'Main-Class': mainClassName
  }
}
